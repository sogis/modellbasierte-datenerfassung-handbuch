plugins {
    id 'org.asciidoctor.jvm.convert' version '4.0.3'
    id 'org.asciidoctor.jvm.pdf' version '4.0.3'
    id "de.undercouch.download" version "5.6.0"
    id 'org.jreleaser' version '1.13.1'
}

apply from: "$rootDir/gradle/versioning.gradle"

repositories {
    mavenCentral()
}

asciidoctor {
    sourceDir  file('src/docs/asciidoc')
    outputDir  file('build/docs')

    asciidoctorj {
        //version = '2.5.13' // Default wird 2.5.7 verwendet.

        attributes 'encoding': 'utf-8',
            'toc' : 'right',
            'toc-title': 'Inhaltsverzeichnis',
            'author': 'Amt für Geoinformation Kanton Solothurn',
            'email': 'agi@bd.so.ch',
            'chapter-label': '',
            'sectnums':  '',
            'sectlinks': '',
            'sectanchors': '',
            'figure-caption': 'Abbildung',
            'stylesheet' : file('./sogis.css')

        options doctype: 'book', ruby: 'erubis'
    }
}

task downloadCss(type: Download) {
    src([
        'https://raw.githubusercontent.com/sogis/sogis-docs-asciidoctor-themes/master/html/sogis-simple/sogis.css'
    ])
    dest file("./sogis.css")
}

task deleteCss(type: Delete) {
    delete './sogis.css'
}

asciidoctor.dependsOn downloadCss
asciidoctor.finalizedBy deleteCss

pdfThemes {
    github 'sogis-frutiger', { 
        organisation = 'sogis' 
        repository = 'sogis-docs-asciidoctor-themes' 
        relativePath = 'pdf/sogis-frutiger' 
        branch = 'master' 
    }
}

asciidoctorPdf {
    executionMode = JAVA_EXEC

    forkOptions {
       setMaxHeapSize '1024m'
    }

    asciidoctorj {
        attributes 'imagesdir' : 'src/docs/asciidoc/images',
            'toc' : '',
            'toc-title' : 'Inhaltsverzeichnis',
            'author': 'Amt für Geoinformation Kanton Solothurn',
            'email': 'agi@bd.so.ch',
            'figure-caption' : 'Abbildung',
            'sectnums' : '',
            'chapter-signifier' : '',
            'revdate' : getDate()

        options doctype: 'book', ruby: 'erubis'
    }
    
    theme 'sogis-frutiger'
    fontsDirs file("fonts/")

    doLast {
        file("${outputDir}/index.pdf").renameTo("${outputDir}/modellbasierte-datenerfassung.pdf")
    }
}

// Fonts müssen vorhanden sein und können nicht mit der Theme mitgeliefert werden.
task downloadFonts(type: Download) {
    src([
        'https://github.com/sogis/sogis-docs-asciidoctor-themes/raw/master/pdf/sogis-frutiger/fonts/frutiger-normal.ttf',
        'https://github.com/sogis/sogis-docs-asciidoctor-themes/raw/master/pdf/sogis-frutiger/fonts/frutiger-bold.ttf',
        'https://github.com/sogis/sogis-docs-asciidoctor-themes/raw/master/pdf/sogis-frutiger/fonts/frutiger-italic.ttf',
        'https://github.com/sogis/sogis-docs-asciidoctor-themes/raw/master/pdf/sogis-frutiger/fonts/frutiger-bold_italic.ttf'
    ])
    dest file("fonts/")
}

task deleteFonts(type: Delete) {
    delete 'fonts'
}

asciidoctorPdf.dependsOn downloadFonts
asciidoctorPdf.finalizedBy deleteFonts

def getDate() {
    return new Date().format('yyyy-MM-dd HH:mm')
}

jreleaser {
    project {
        name = 'modellbasierte-datenerfassung-handbuch'
        description = 'ÖREB-Handbuch'
        website = 'https://agi.so.ch'
        authors = ['edigonzales']
        license = 'MIT'
        docsUrl = 'https://github.com/sogis/modellbasierte-datenerfassung-handbuch'
    }
    release {
        github {
            branch = 'master'
            //owner = 'sogis'
            overwrite = true
            apiEndpoint = 'https://api.github.com'
        }
    }
    files {
        artifact {
            path = 'build/docs/asciidocPdf/modellbasierte-datenerfassung.pdf'
        }
    }
}