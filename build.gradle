import org.asciidoctor.gradle.AsciidoctorTask

plugins {
    id "org.asciidoctor.convert" version "1.5.6"
}

apply from: "$rootDir/gradle/versioning.gradle"
apply plugin: 'groovy'

dependencies {
    testCompile(
        'junit:junit:4.12',
        'org.codehaus.groovy:groovy-all:2.4.9',
        'org.spockframework:spock-core:1.1-groovy-2.4'
    )
    asciidoctor 'org.asciidoctor:asciidoctorj-pdf:1.5.0-alpha.16'
}

asciidoctorj {
    version = '1.5.8.1'
}

ext {
    currentDate = new Date().format("d. MMM yyyy")
}

asciidoctor {
    attributes = [
        doctype: 'book',
        icons: 'font',
        sectlink: true,
        sectanchors: true,
        numbered: true,
        'source-highlighter': 'coderay',
        'currentDate': "$currentDate"
   ]
}

tasks.withType(AsciidoctorTask) { docTask ->

    attributes \
          'source-highlighter': 'coderay',
          'toc': 'right',
          'icons': 'font',
          'sourceDir': "$projectDir/src/",
          'project-version' : project.version.toString()

    // good to see what the build is doing...
    logDocuments = true
}

task generatePDF (
    type: AsciidoctorTask,
    description: 'use pdf as asciidoc backend') {

    backends = ['pdf']
    attributes \
        'pdf-stylesdir': "$projectDir/src/docs/asciidoc/theme",
        'pdf-style': 'custom',
        'pdf-fontsdir': "$projectDir/src/docs/asciidoc/theme"
}

defaultTasks 'clean', 'asciidoctor'

task fubar() {
    doLast {
        println version
    }
}